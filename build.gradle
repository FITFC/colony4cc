buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.1.+', changing: true
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

def extraModsDir = "mods-${minecraft_version}"

version = "${mod_version}"
group = 'mirality'
archivesBaseName = "Colony4ComputerCraft-${minecraft_version}"

java.toolchain.languageVersion = JavaLanguageVersion.of(8) // Mojang ships Java 8 to end users, so your mod should target Java 8.

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    mappings channel: 'official', version: "${minecraft_version}"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
    
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')
            arg "-mixin.config=colony4cc.mixins.json"

            //property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.markers', ''
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            mods {
                colony4cc {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            arg "-mixin.config=colony4cc.mixins.json"

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            mods {
                colony4cc {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            args '--mod', 'colony4cc', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/'), '--existing-mod', 'computercraft', '--existing-mod', 'structurize', '--existing-mod', 'minecolonies'

            mods {
                colony4cc {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets {
    main.resources.srcDirs += 'src/generated/resources'
}

mixin {
    add sourceSets.main, "colony4cc.refmap.json"
}

processResources {
    inputs.property 'version', project.version

    from(sourceSets.main.resources.srcDirs) {
        include 'META-INF/mods.toml'
        expand 'version': project.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'META-INF/mods.toml'
    }
}

repositories {
    flatDir {
        name "extra-mods"
        dir file(extraModsDir)
        content {
            includeGroup 'extra-mods'
        }
    }
    mavenCentral()
    maven {
        name 'TOP'
        url 'https://maven.tterrag.com/'
        content {
            includeGroup 'mcjty.theoneprobe'
        }
    }
    maven {
        name 'HWYLA'
        url 'https://maven.tehnut.info'
        content {
            includeGroup 'mcp.mobius.waila'
        }
    }
    maven {
        name 'JEI'
        url 'https://dvs1.progwml6.com/files/maven'
        content {
            includeGroup 'mezz.jei'
        }
    }
    maven {
        name 'MineColonies'
        url 'https://ldtteam.jfrog.io/ldtteam/modding'
        content {
            includeGroup 'com.minecolonies'
            includeGroup 'com.ldtteam'
        }
    }
    maven {
        name 'CC Tweaked'
        url 'https://squiddev.cc/maven'
        content {
            includeGroup 'org.squiddev'
        }
    }
    maven {
        name 'CurseMaven'
        url 'https://www.cursemaven.com'
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    compile fg.deobf("org.squiddev:cc-tweaked-${cc_version}")
    //compile(fg.deobf("com.minecolonies:minecolonies:${colony_version}:universal") {
    compile(fg.deobf("curse.maven:minecolonies-245506:3543467") {
        exclude group: "mezz.jei", module: "*"
    })
    //compile fg.deobf("com.ldtteam:structurize:${structurize_version}:universal")
    compile fg.deobf("curse.maven:structurize-298744:3488829")
    runtimeOnly fg.deobf("com.ldtteam:datagenerators:${datagen_version}")

    compileOnly fg.deobf("mcjty.theoneprobe:TheOneProbe-${top_version}:api")
    runtimeOnly fg.deobf("mcjty.theoneprobe:TheOneProbe-${top_version}")
    compileOnly fg.deobf("mcp.mobius.waila:Hwyla:${hwyla_version}:api")
    runtimeOnly fg.deobf("mcp.mobius.waila:Hwyla:${hwyla_version}")
    compileOnly fg.deobf("mezz.jei:jei-${jei_version}:api")
    runtimeOnly fg.deobf("mezz.jei:jei-${jei_version}")

    compileOnly 'com.intellij:annotations:+@jar'

    // extra mods for testing
    for (mod in fileTree(dir: extraModsDir, include: '*.jar')) {
        def basename = mod.name.substring(0, mod.name.length() - ".jar".length())
        def sep = basename.lastIndexOf('-')
        assert sep != -1
        def name = basename.substring(0, sep)
        def version = basename.substring(sep + 1)
        runtimeOnly fg.deobf("extra-mods:$name:$version")
    }

    annotationProcessor 'org.spongepowered:mixin:0.8.2:processor'
}

jar {
    manifest {
        attributes([
            "Specification-Title": "colony4cc",
            "Specification-Vendor": "Mirality",
            "Specification-Version": "1",
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :"Mirality",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "MixinConfigs": "colony4cc.mixins.json"
        ])
    }
}

jar.finalizedBy('reobfJar') 

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier 'deobf'
}

artifacts {
    archives jar
    archives sourcesJar
    archives deobfJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = project.archivesBaseName
            version = project.version
            artifact jar
            artifact sourcesJar
            artifact deobfJar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}